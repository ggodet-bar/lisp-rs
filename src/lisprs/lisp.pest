number = @{ "-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT*)? }
symbol = @{
    ("+" | "*" | "/" | "_" | "-" | ">=" | "<=" | "<>" | ">" | "<" | "==" | "="  | ASCII_ALPHA)+ }
string = @{ "\"" ~ string_character* ~ "\"" }
string_character = {
    !"\""        // if the following text is not a quote
    ~ ANY         // then consume one character
}

atom = _{ symbol | number | string | sexpr | dotted_list }
quoted_atom = { "'"  ~ atom}
atom_type = _{ quoted_atom | atom }
dotted_list = { "(" ~ atom_type ~ "." ~ atom_type ~ ")" }
sexpr = { "(" ~ atom_type* ~ ")" }
lisp = { SOI ~ atom_type* ~ EOI }

WHITESPACE = _{ " " | "\t" | "\n" }
COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }